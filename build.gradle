plugins {
	id 'java'
	// ShadowJar
	id 'com.github.johnrengelman.shadow' version '8.1.1'
}

// Getting version
apply from: 'version.gradle'

def version = loadVersion().getVersion()
def mainClassName = 'dev.fileeditor.votl.Main'

repositories {
	// Use Maven Central for resolving dependencies
	mavenCentral()
}

dependencies {
	// JDA library and additions
	// https://mvnrepository.com/artifact/net.dv8tion/JDA
	implementation('net.dv8tion:JDA:5.1.0') {				// JDA library
		exclude module: 'opus-java'
	}
	// https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
	implementation 'ch.qos.logback:logback-classic:1.5.7'			// Logging (required by JDA)

	// Tools
	// https://mvnrepository.com/artifact/com.jayway.jsonpath/json-path
	implementation 'com.jayway.jsonpath:json-path:2.9.0'			// json path-er
	// https://mvnrepository.com/artifact/org.xerial/sqlite-jdbc
	implementation 'org.xerial:sqlite-jdbc:3.46.1.0'				// sqlite database
	// https://mvnrepository.com/artifact/org.jsoup/jsoup
	implementation 'org.jsoup:jsoup:1.18.1'							// HTML tools
	// https://mvnrepository.com/artifact/io.github.java-diff-utils/java-diff-utils
	implementation 'io.github.java-diff-utils:java-diff-utils:4.12'
	// https://mvnrepository.com/artifact/org.json/json
    implementation 'org.json:json:20240303'

	// Eval
	// https://mvnrepository.com/artifact/org.apache.groovy/groovy
	implementation 'org.apache.groovy:groovy:4.0.22'				// Groovy - code evaluation

	// Backend
	// https://mvnrepository.com/artifact/io.javalin/javalin
	implementation 'io.javalin:javalin:6.3.0'
	// https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'
}

java {
	sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17
}

javadoc {
	options.tags = ['implNote:a:Implementation Note:']
}

// jar's Manifest
jar {
	manifest {
		attributes 'Implementation-Title': 'VOTL Bot',
				   'Implementation-Version': version,
				   'Implementation-Vendor': 'FiLe group',
				   'Main-Class': mainClassName
	}
}

// ShadowJar
shadowJar {
	archiveBaseName.set('VOTL')
	archiveVersion.set(version)
	archiveClassifier.set('')
}

compileJava.options.encoding = 'UTF-8'
