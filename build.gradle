plugins {
	id 'java'
	// ShadowJar
	// https://mvnrepository.com/artifact/com.gradleup.shadow/shadow-gradle-plugin
	id 'com.gradleup.shadow' version '9.0.0-beta12'
}

// Getting version
apply from: 'version.gradle'

String version = loadVersion().getVersion()
String mainClassName = 'dev.fileeditor.votl.Main'

repositories {
	// Use Maven Central for resolving dependencies
	mavenCentral()
	flatDir {
		dirs 'libs'
	}
}

dependencies {
	// JDA library and additions
	// https://mvnrepository.com/artifact/net.dv8tion/JDA
	implementation('net.dv8tion:JDA:5.4.0') {						// JDA library
		exclude module: 'opus-java'
	}
	// https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
	implementation 'ch.qos.logback:logback-classic:1.5.18'			// Logging (required by JDA)

	// Tools
	// https://mvnrepository.com/artifact/com.jayway.jsonpath/json-path
	implementation 'com.jayway.jsonpath:json-path:2.9.0'			// json path-er
	// https://mvnrepository.com/artifact/org.xerial/sqlite-jdbc
	implementation 'org.xerial:sqlite-jdbc:3.49.1.0'				// sqlite database
	// https://mvnrepository.com/artifact/org.jsoup/jsoup
	implementation 'org.jsoup:jsoup:1.19.1'							// HTML tools
	// https://mvnrepository.com/artifact/io.github.java-diff-utils/java-diff-utils
	implementation 'io.github.java-diff-utils:java-diff-utils:4.15'	// Diff finder
	// https://mvnrepository.com/artifact/org.json/json
	implementation 'org.json:json:20250107'							// JsonObject
	// https://mvnrepository.com/artifact/com.github.ben-manes.caffeine/caffeine
	implementation 'com.github.ben-manes.caffeine:caffeine:3.2.0'	// Cache

	// Eval
	// https://mvnrepository.com/artifact/org.apache.groovy/groovy
	implementation 'org.apache.groovy:groovy:4.0.26'				// Groovy - code evaluation

    // Metrics
    // https://mvnrepository.com/artifact/io.github.classgraph/classgraph
    implementation 'io.github.classgraph:classgraph:4.8.179'        // Class lookup

	// Backend
	// https://mvnrepository.com/artifact/io.javalin/javalin
	implementation 'io.javalin:javalin:6.6.0'
	// https://mvnrepository.com/artifact/com.fasterxml.jackson.core/jackson-databind
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.18.3'

	// OAuth2
	implementation 'jda-oauth2:jda-oauth2:1.0.0'
}

java {
	sourceCompatibility = targetCompatibility = JavaVersion.VERSION_21
}

// jar's Manifest
jar {
	manifest {
		attributes 'Implementation-Title': 'VOTL Bot',
				   'Implementation-Version': version,
				   'Implementation-Vendor': 'FiLe group',
				   'Main-Class': mainClassName
	}
}

// ShadowJar
shadowJar {
	archiveBaseName.set('VOTL')
	archiveClassifier.set('')
	if(project.hasProperty('runId')) {
		archiveVersion.set("${version}-SNAPSHOT-${project.property('runId')}")
	} else {
		archiveVersion.set(version)
	}
	minimize {
		exclude(dependency('org.apache.groovy:groovy:.*'))
		exclude(dependency('ch.qos.logback:logback-classic:.*'))
		exclude(dependency('org.xerial:sqlite-jdbc:.*'))
		exclude(dependency('com.github.ben-manes.caffeine:caffeine:.*'))
	}
}

compileJava.options.encoding = 'UTF-8'
